include "std"

// Location(file: str, row: int, col: int)
ENUM Location.size 8 :
  Location.file   // str
  Location.row    // int
  Location.column // int
END

// Initialize Location from parameters
// Params: Location.file, Location.row, Location.column
// Return: Pointer to Location
function Location.init str int int -> ptr :
  // Allocate memory for Location
  Location.size malloc

  // Load the parameters to the allocated memory
  swap over Location.file    ptr+ str.store
  swap over Location.row     ptr+ int.store
  swap over Location.column  ptr+ int.store
end

// Get Location.file
// Params: Pointer to Location (PTR)
// Return: Location.file (STR)
function Location.get_file ptr -> str :
  str.load
end

// Get Location.row
// Params: Pointer to Location (PTR)
// Return: Location.row (INT)
function Location.get_row ptr -> int :
  Location.row ptr+ int.load
end

// Get Location.column
// Params: Pointer to Location (PTR)
// Return: Location.column (STR)
function Location.get_column ptr -> int :
  Location.column ptr+ int.load
end

function Location.repr ptr -> str :
  take location in
  location Location.get_file
  ","                               str.cat
  location Location.get_row itoa    str.cat
  ","                               str.cat
  location Location.get_column itoa str.cat
end

function Location.print ptr :
  "Location.file      => "  puts
  dup Location.file         ptr+
  str.load puts "\n"        puts

  "Location.row       => "  puts
  dup Location.row          ptr+
  int.load print "\n"       puts

  "Location.column    => "  puts
      Location.column       ptr+
  int.load print "\n"       puts
end
