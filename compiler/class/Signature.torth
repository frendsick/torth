include "std"
include "compiler/defs"
include "compiler/utils"
class Signature
  params:List
  returns:List

  method init param_types:List return_types:List -> Signature :
    Signature.size malloc cast(Signature)
    take signature in

    param_types   signature Signature->params
    return_types  signature Signature->returns
    signature
  end

  method repr self:Signature -> str :
    self Signature.returns
    dup List.len
    self Signature.params
    dup List.len
    ""    str.copy
    dup   str.copy
    0
    take
      index
      params_repr
      returns_repr
      params_len
      params
      returns_len
      returns
    in

    // Set empty Signature parts as None
    if params_len 0 == do
      "None" params_repr =
    endif
    if returns_len 0 == do
      "None" returns_repr =
    endif

    // Generate repr from Signature.params
    while index params_len < do
      index params List.nth str.load
      if index params_len 1 - < do
        " " str.cat
      endif
      params_repr swap str.cat params_repr =
      index 1 + index =
    done

    // Generate repr from Signature.returns
    0 index =
    while index returns_len < do
      index returns List.nth str.load
      if index returns_len 1 - < do
        " " str.cat
      endif
      returns_repr swap str.cat returns_repr =
      index 1 + index =
    done
    params_repr " -> "  str.cat
    returns_repr        str.cat
  end
endclass
