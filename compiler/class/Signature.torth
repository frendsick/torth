include "std"
include "compiler/defs"
include "compiler/utils"
class Signature
  params:List
  returns:List

  method init param_types:List return_types:List -> Signature :
    Signature.size malloc cast(Signature)
    take signature in

    param_types   signature Signature->params
    return_types  signature Signature->returns
    signature
  end

  method from_repr repr:str -> Signature :
    List.init
    List.init
    "" str.copy
    0
    "->" repr str.find
    take
      arrow_index
      index
      buffer
      param_types
      return_types
    in

    while index arrow_index < do
      index repr str.char_at
      take current_char in

      if current_char char.is_whitespace do
        buffer param_types List.append
        "" str.copy buffer =
      else
        current_char buffer str.append buffer =
      endif

      index 1 + index =
    done

    // Parse over the arrow token
    index "-> " str.len + index =

    while index repr str.char_at NULL != do
      index repr str.char_at
      take current_char in

      if current_char char.is_whitespace do
        buffer return_types List.append
        "" str.copy buffer =
      else
        current_char buffer str.append buffer =
      endif

      index 1 + index =
    done

    if buffer "" streq not do
      buffer return_types List.append
    endif

    return_types param_types Signature.init
  end

  method repr self:Signature -> str :
    self Signature.returns
    dup List.len
    self Signature.params
    dup List.len
    ""    str.copy
    dup   str.copy
    0
    take
      index
      params_repr
      returns_repr
      params_len
      params
      returns_len
      returns
    in

    // Set empty Signature parts as None
    if params_len 0 == do
      "None" params_repr =
    endif
    if returns_len 0 == do
      "None" returns_repr =
    endif

    // Generate repr from Signature.params
    while index params_len < do
      index params List.nth str.load str.copy
      if index params_len 1 - < do
        " " str.cat
      endif
      params_repr swap str.cat params_repr =
      index 1 + index =
    done

    // Generate repr from Signature.returns
    0 index =
    while index returns_len < do
      index returns List.nth str.load str.copy
      if index returns_len 1 - < do
        " " str.cat
      endif
      returns_repr swap str.cat returns_repr =
      index 1 + index =
    done
    params_repr " -> "  str.cat
    returns_repr        str.cat
  end
endclass
