// error.torth - Library of errors used by the self-hosted Torth compiler
include "std"
include "compiler/defs"
include "compiler/utils"

// Params: Error type (STR), message (STR)
function print_error error_type:str message:str :
  f"{Color.HEADER}Compiler Error {Color.ERROR}{error_type}{Color.RESET}:{message}\n" eputs
end

// Params: Error type (STR), message (STR)
// Return: NoReturn
function CompilerError str str :
  print_error
  1 exit
end

// Params:
//    error_type: str
//    message: str
//    token: Token
// Return: NoReturn
function CompilerErrorWithToken str str Token :
  print_error "\n"  eputs
  get_token_info    eputs
  1 exit
end

// Params:
//    error_type: str
//    message: str
//    token: Token
//    type_stack: TypeStack
// Return: NoReturn
function CompilerErrorWithStack
  error_type:str
  message:str
  token:Token
  type_stack:Node
:
  message error_type print_error
  "\nCurrent stack state:\n"  eputs
  type_stack TypeStack.repr   eputs
  "\n"                        eputs
  token get_token_info        eputs
  1 exit
end

// Params: Error message (STR)
// Return: NoReturn
function NotImplementedError str :
  "NOT_IMPLEMENTED" CompilerError
end
