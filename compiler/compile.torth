include "std"
include "compiler/error"
include "compiler/utils"

// Compile and link the assembly file with GAS and LD
// Params: out_file (STR)
function compile_executable out_file:str args:Argparser :
  "-g" args Argparser.flag_present
  take is_debug in

  // Compile assembly code file to an object file with GAS
  if is_debug do
    out_file compile_debug_with_gas& exec_forked
  else
    out_file compile_with_gas& exec_forked
  endif

  // Link the object file to an executable with LD
  if is_debug do
    out_file link_debug_with_ld& exec_forked
  else
    out_file link_with_ld& exec_forked
  endif
end

// Compile assembly code file to object file with GAS
function compile_with_gas out_file:str :
  out_file str.copy ".o"   str.cat
  out_file str.copy ".asm" str.cat
  List.init
  take
    argv
    asm_file
    object_file
  in

  c"as"                   cast(ptr) argv List.append
  c"-o"                   cast(ptr) argv List.append
  object_file str.to_cstr cast(ptr) argv List.append
  asm_file    str.to_cstr cast(ptr) argv List.append
  NULLPTR argv List.first c"/usr/bin/as" execve drop
end

// Compile assembly code file to object file with debug information with GAS
function compile_debug_with_gas out_file:str :
  out_file str.copy ".o"   str.cat
  out_file str.copy ".asm" str.cat
  List.init
  take
    argv
    asm_file
    object_file
  in

  c"as"                   cast(ptr) argv List.append
  c"-g"                   cast(ptr) argv List.append
  c"-o"                   cast(ptr) argv List.append
  object_file str.to_cstr cast(ptr) argv List.append
  asm_file    str.to_cstr cast(ptr) argv List.append
  NULLPTR argv List.first c"/usr/bin/as" execve drop
end

// Link object file using `ld`
function link_with_ld out_file:str :
  out_file str.copy ".o" str.cat
  List.init
  take argv object_file in

  c"ld"                       cast(ptr) argv List.append
  c"-melf_x86_64"             cast(ptr) argv List.append
  c"-s"                       cast(ptr) argv List.append
  c"-o"                       cast(ptr) argv List.append
  out_file        str.to_cstr cast(ptr) argv List.append
  object_file     str.to_cstr cast(ptr) argv List.append
  NULLPTR argv List.first c"/usr/bin/ld" execve drop
end

// Link object file with debug information using `ld`
function link_debug_with_ld out_file:str :
  out_file str.copy ".o" str.cat
  List.init
  take argv object_file in

  c"ld"                       cast(ptr) argv List.append
  c"-melf_x86_64"             cast(ptr) argv List.append
  c"-g"                       cast(ptr) argv List.append
  c"-o"                       cast(ptr) argv List.append
  out_file        str.to_cstr cast(ptr) argv List.append
  object_file     str.to_cstr cast(ptr) argv List.append
  NULLPTR argv List.first c"/usr/bin/ld" execve drop
end

// Remove files generated during compilation
// Params
//    out_file: str
// Return None
function remove_compilation_files out_file:str :
  // Remove Assembly file
  f"{out_file}.asm" remove_file& exec_forked

  // Remove object file
  f"{out_file}.o" remove_file& exec_forked
end
