include "std" // Imports malloc & puts functions
class Dog
  // Attributes
  name:str
  breed:str
  says:str

  method init name:str breed:str -> ptr :
    // Allocate memory for Dog object
    Dog.size malloc
    take self in

    // Set object attribute values with arrow operator "->"
    name    self Dog->name
    breed   self Dog->breed
    "Woof"  self Dog->says // Dog always says "Woof" unless set otherwise later
    self // return Dog object
  end

  // Prints "<Dog.breed> <Dog.name> says <Dog.says>" with a newline at the end
  // Takes Dog object as parameter
  method talk self:ptr :
    // Get object attribute values with dot operator "."
    self Dog.breed  puts
    " "             puts
    self Dog.name   puts
    " says "        puts
    self Dog.says   puts
    "\n"            puts
  end
endclass

function main :
  // Create two Dog objects
  "Golden retriever" "Gooby" Dog.init
  "Bull terrier"     "Rocky" Dog.init
  take rocky gooby in

  // Terrier growls
  "Grrrr" rocky Dog->says

  // Call Dog.talk method with the objects
  gooby Dog.talk
  rocky Dog.talk
end
