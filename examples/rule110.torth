include "std"
const RULES_COUNT 8 end
const BOARD_SIZE 40 end

memory rules int.size end             // uint8 rules[8]
memory current_board BOARD_SIZE end   // int current_board[BOARD_SIZE]
memory previous_board BOARD_SIZE end  // int previous_board[BOARD_SIZE]
memory pattern 3 end                  // int pattern[3]

function initialize_rules :
  rules                     // Pointer to table
  0 over        store_byte  // table[0] == 0  // 111
  1 over 1 ptr+ store_byte  // table[1] == 1  // 110
  1 over 2 ptr+ store_byte  // table[2] == 1  // 101
  0 over 3 ptr+ store_byte  // table[3] == 0  // 100
  1 over 4 ptr+ store_byte  // table[4] == 1  // 011
  1 over 5 ptr+ store_byte  // table[5] == 1  // 010
  1 over 6 ptr+ store_byte  // table[6] == 1  // 001
  0 over 7 ptr+ store_byte  // table[7] == 0  // 000
  drop
end

function initialize_board :
  current_board
  u1 over 2 ptr+ store_byte   // current_board[2] = 1
  drop
end

// Sync previous_board with current_board
function sync_boards :
  0 while dup BOARD_SIZE < do
    current_board over ptr+ load_byte
    over previous_board swap ptr+ store_byte
    1 +
  done drop
end

function print_iteration :
  // j = 0; while(i < BOARD_SIZE - 2)
  0 while dup BOARD_SIZE < do

    // if(current_board[j] == 1)
    if current_board over ptr+ load_byte 1 == do
      "*" puts
    else
      " " puts
    endif
    1 +   // j++
  done drop
  "\n" puts
end

function get_pattern int -> int :
  previous_board
  over 1 - over swap ptr+ load_byte pattern        store_byte  // pattern[0] = previous_board[j-1]
  over     over swap ptr+ load_byte pattern 1 ptr+ store_byte  // pattern[1] = previous_board[j]
  over 1 + over swap ptr+ load_byte pattern 2 ptr+ store_byte  // pattern[2] = previous_board[j+1]
  drop
end

function output_board :
  "Board iteration: " puts
  0 while dup BOARD_SIZE < do
    current_board over ptr+ char.load cast(int) putu
    1 +
  done drop "\n" puts
 end

function set_board_index int int :
  take rule_index board_index in
  rules rule_index ptr+ load_byte
  current_board board_index ptr+ store_byte
end

// Sets the current_board index according to rules
function update_board_index int :
  // Save the integer from pattern Memory
  pattern int.load
  take int.pattern in

  // Set the board index according to the certain pattern and return
  if   int.pattern 0x10101 == do // 111
    0 set_board_index return
  elif int.pattern 0x10100 == do // 110
    1 set_board_index return
  elif int.pattern 0x10001 == do // 101
    2 set_board_index return
  elif int.pattern 0x10000 == do // 100
    3 set_board_index return
  elif int.pattern 0x00101 == do // 011
    4 set_board_index return
  elif int.pattern 0x00100 == do // 010
    5 set_board_index return
  elif int.pattern 0x00001 == do // 001
    6 set_board_index return
  elif int.pattern 0x00000 == do // 000
    7 set_board_index return
  endif
  "Unknown pattern " puts int.pattern putu "\n" puts
  drop
end

function calculate_next_iteration :
  // j = 1; while(j < BOARD_SIZE - 1)
  1 while dup BOARD_SIZE < do
    get_pattern
    dup update_board_index
    1 +  // j++
  done drop
end

function rule110 :
  // i = 0; while(i < BOARD_SIZE - 2)
  0 while dup BOARD_SIZE 2 - < do
    sync_boards // Copy current_board to previous_board
    print_iteration
    calculate_next_iteration
    1 +  // i++
  done drop
end

function main :
  initialize_rules
  initialize_board
  rule110
end
