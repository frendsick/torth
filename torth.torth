// torth.torth - Self hosted implementation of the Torth compiler
include "std"
include "compiler/asm"
include "compiler/compile"
include "compiler/error"
include "compiler/lex"
include "compiler/program"

function main :

  // Get the code file from command line (first argument)
  1 get_nth_cmd_line_argument
  take code_file in

  // Get the current directory
  "PWD" getenv
  take compiler_directory in

  // Get a list of included files for the program
  "[INFO] Getting included files\n" puts
  compiler_directory code_file get_included_files
  take included_files in

  "[INFO] Parsing Tokens\n" puts
  included_files get_token_matches_from_files
  take token_matches in

  "[INFO] Parsing Constants\n" puts
  token_matches get_constants
  take constants in

  "[INFO] Parsing Memories\n" puts
  constants token_matches get_memories
  take memories in

  "[INFO] Parsing Functions\n" puts
  memories constants token_matches get_functions
  take functions in

  // Get the intermediate representation (Program) of each Function
  "[INFO] Parsing Sub-Programs\n" puts
  memories constants functions get_sub_programs
  take sub_programs in

  // TODO: Type check the Program
  // program type_check_program

  "[INFO] Generating Assembly code\n" puts
  // Generate assembly code from the intermediate representation
  memories sub_programs generate_assembly_code
  take assembly_code in

  "[INFO] Compiling Assembly code\n" puts
  // Write the assembly code to a file
  assembly_code mode_644 "test.asm" write_file drop

  // Compile the assembly code file to a Linux x86_64 executable
  compile_executable
end
