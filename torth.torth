// torth.torth - Self hosted implementation of the Torth compiler
include "std"
include "argparser"
include "compiler/asm"
include "compiler/compile"
include "compiler/error"
include "compiler/lex"
include "compiler/program"
include "compiler/typecheck"
include "compiler/utils"

function main :
  // Parse command line arguments
  parse_command_line_arguments
  take args in

  // Get the code file name from command line
  args get_code_file_name
  take code_file in

  // Get a list of included files for the program
  "[INFO] Getting included files\n" puts
  code_file get_included_files
  take included_files in

  // Parse diffent code components
  "[INFO] Parsing code from " puts
  code_file puts "\n" puts
  included_files get_token_matches_from_files
  take token_matches in

  // Parse Constants
  token_matches get_constants
  take constants in

  // Parse Memories
  constants token_matches get_memories
  take memories in

  // Parse Functions
  memories constants token_matches get_functions
  take functions in

  // Get the intermediate representation (Program) of each Function
  memories constants functions get_sub_programs
  take sub_programs in

  // Type check the Program
  "[INFO] Type checking the program\n" puts
  functions sub_programs type_check_application

  // Generate assembly code from the intermediate representation
  "[INFO] Generating Assembly code\n" puts
  args code_file get_output_file_name
  take out_file in
  memories sub_programs out_file ".asm" str.cat generate_assembly_file

  // Compile the assembly code file to a Linux x86_64 executable
  "[INFO] Compiling Assembly code\n" puts
  out_file compile_executable

  // Remove files generated during compilation
  // if -s command line flag is not present
  if "-s" args Argparser.flag_present not do
    "[INFO] Removing files generated during compilation\n" puts
    out_file remove_compilation_files
  endif

  // Run the program if -r command line flag is present
  if "-r" args Argparser.flag_present do
    "[INFO] Running the program\n" puts
    out_file run_program
  endif
end
